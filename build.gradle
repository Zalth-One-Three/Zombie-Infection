buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

ext.configFile = file('build.properties')

ext.config = parseConfig(configFile)

project.ext.set("buildnum", "local")
version = "${config.mc_version}-${config.mod_version_major}.${config.mod_version_minor}"
project.buildnum = "${config.mod_version_build}"
version += "." + project.buildnum

group= "${config.group}"
archivesBaseName = "${config.archive_base}"

minecraft {
    version = "${config.mc_version}-${config.forge_version}"
    runDir = "eclipse"
    if (config.mappings == "today") {
	    mappings = "snapshot_" + getDate()
	} else {
		mappings = "${config.mappings}"
	}
}

repositories {
    maven {
        name 'ForgeFS'
        url 'http://files.minecraftforge.net/maven'
     }
}

dependencies {}

processResources
{
    inputs.property "version", project.buildnum
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.buildnum, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}

def getDate() {
	def date = new Date()
	def formattedDate = date.format('yyyyMMdd')
	return formattedDate
}